plugins {
	id 'com.android.application'
}

configurations.configureEach {
	exclude group: 'androidx.collection', module: 'collection'
	exclude group: 'androidx.concurrent', module: 'concurrent-futures'
	exclude group: 'androidx.viewpager', module: 'viewpager'
	exclude group: 'com.google.guava', module: 'listenablefuture'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

base {
	archivesName = "techidon"
}

android {
	compileSdk = 35
	defaultConfig {
		manifestPlaceholders = [oAuthScheme:"techidon-android-auth"]
		applicationId = "de.icod.techidon"
		minSdk = 23
		targetSdk = 35
		versionCode = 10001
		versionName = "1.0.1"
		testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
		resourceConfigurations += ['ar-rSA', 'ar-rDZ', 'be-rBY', 'bn-rBD', 'bs-rBA', 'ca-rES', 'cs-rCZ', 'da-rDK', 'de-rDE', 'el-rGR', 'es-rES', 'eu-rES', 'fa-rIR', 'fi-rFI', 'fil-rPH', 'fr-rFR', 'ga-rIE', 'gd-rGB', 'gl-rES', 'hi-rIN', 'hr-rHR', 'hu-rHU', 'hy-rAM', 'ig-rNG', 'in-rID', 'is-rIS', 'it-rIT', 'iw-rIL', 'ja-rJP', 'kab', 'ko-rKR', 'my-rMM', 'nl-rNL', 'no-rNO', 'oc-rFR', 'pl-rPL', 'pt-rBR', 'pt-rPT', 'ro-rRO', 'ru-rRU', 'si-rLK', 'sl-rSI', 'sv-rSE', 'th-rTH', 'tr-rTR', 'uk-rUA', 'ur-rIN', 'vi-rVN', 'zh-rCN', 'zh-rTW']
	}

	dependenciesInfo {
		// Disables dependency metadata when building APKs.
		includeInApk = false
		// Disables dependency metadata when building Android App Bundles.
		includeInBundle = false
	}

	signingConfigs {
		nightly{
			storeFile = file("keystore/nightly_keystore.jks")
			storePassword System.getenv("SIGNING_STORE_PASSWORD")
			if (storePassword == null) {
				Properties properties = new Properties()
				properties.load(project.rootProject.file('local.properties').newDataInputStream())
				storePassword = properties.getProperty('SIGNING_STORE_PASSWORD')
			}
			keyAlias System.getenv("SIGNING_KEY_ALIAS")
			if (keyAlias == null) {
				Properties properties = new Properties()
				properties.load(project.rootProject.file('local.properties').newDataInputStream())
				keyAlias = properties.getProperty('SIGNING_KEY_ALIAS')
			}
			keyPassword System.getenv("SIGNING_KEY_PASSWORD")
			if (keyPassword == null) {
				Properties properties = new Properties()
				properties.load(project.rootProject.file('local.properties').newDataInputStream())
				keyPassword = properties.getProperty('SIGNING_KEY_PASSWORD')
			}
		}

		release{
			storeFile = file("keystore/upload_keystore.jks")
			storePassword System.getenv("UPLOAD_SIGNING_STORE_PASSWORD")
			if (storePassword == null) {
				Properties properties = new Properties()
				properties.load(project.rootProject.file('local.properties').newDataInputStream())
				storePassword = properties.getProperty('UPLOAD_SIGNING_STORE_PASSWORD')
			}
			keyAlias System.getenv("UPLOAD_SIGNING_KEY_ALIAS")
			if (keyAlias == null) {
				Properties properties = new Properties()
				properties.load(project.rootProject.file('local.properties').newDataInputStream())
				keyAlias = properties.getProperty('UPLOAD_SIGNING_KEY_ALIAS')
			}
			keyPassword System.getenv("UPLOAD_SIGNING_KEY_PASSWORD")
			if (keyPassword == null) {
				Properties properties = new Properties()
				properties.load(project.rootProject.file('local.properties').newDataInputStream())
				keyPassword = properties.getProperty('UPLOAD_SIGNING_KEY_PASSWORD')
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled = true
			shrinkResources = true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig = signingConfigs.release
		}
		debug{
			debuggable = true
			versionNameSuffix = '-debug'
			applicationIdSuffix = '.debug'
			manifestPlaceholders = [oAuthScheme:"techidon-android-debug-auth"]
		}
		nightly{
			if(System.getenv("CURRENT_DATE") != null){
				versionNameSuffix '-nightly+@' + System.getenv("CURRENT_DATE")
			} else {
				Properties properties = new Properties()
				properties.load(project.rootProject.file('local.properties').newDataInputStream())
				versionNameSuffix '-nightly+@' + properties.getProperty('CURRENT_DATE')
			}
			applicationIdSuffix '.nightly'

			signingConfig = signingConfigs.nightly
			matchingFallbacks = ['release']
			manifestPlaceholders = [oAuthScheme:"techidon-android-nightly-auth"]
		}
		playRelease{
			initWith release
			minifyEnabled = true
			shrinkResources = true
			matchingFallbacks = ['release']
		}
		githubRelease {
			initWith release
			versionNameSuffix = '-github'
			matchingFallbacks = ['release']
		}
		fdroidRelease {
			initWith release
			matchingFallbacks = ['release']
			vcsInfo {
				include = true
			}
			// The F-droid build system doesn't like this at all for some reason.
//			versionNameSuffix '-fdroid'
//			signingConfig signingConfigs.release
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
		coreLibraryDesugaringEnabled = true
	}
	sourceSets{
		githubRelease{
			setRoot "src/github"
		}
		debug {
			setRoot "src/debug"
		}
	}
	namespace = 'de.icod.techidon'
	lint {
		abortOnError = false
		checkReleaseBuilds = false
	}

	buildFeatures {
		buildConfig = true
	}
}

dependencies {
	api 'androidx.annotation:annotation:1.6.0'
	implementation 'com.squareup.okhttp3:okhttp:3.14.9'
	implementation 'me.grishka.litex:recyclerview:1.2.1.1'
	implementation 'me.grishka.litex:swiperefreshlayout:1.1.0.1'
	implementation 'me.grishka.litex:browser:1.4.0'
	implementation 'me.grishka.litex:dynamicanimation:1.1.0-alpha03'
	implementation 'me.grishka.litex:viewpager:1.0.0'
	implementation 'me.grishka.litex:viewpager2:1.0.0'
	implementation 'me.grishka.litex:palette:1.0.0'
	implementation project(':appkit')
	implementation 'com.google.code.gson:gson:2.9.0'
	implementation 'org.jsoup:jsoup:1.14.3'
	implementation 'com.squareup:otto:1.3.8'
	implementation 'de.psdev:async-otto:1.0.3'
	implementation 'org.parceler:parceler-api:1.1.12'
	implementation 'com.github.bottom-software-foundation:bottom-java:2.1.0'
	annotationProcessor 'org.parceler:parceler:1.1.12'
	coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
	implementation 'org.unifiedpush.android:connector:3.0.7'

	androidTestImplementation 'androidx.test:core:1.5.0'
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
androidTestImplementation 'androidx.test:runner:1.5.2'
androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
}
